buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

plugins {
    id 'org.jetbrains.grammarkit' version '2021.1.2'
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0-RC'
    id 'org.kordamp.gradle.markdown' version '2.0.0'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'com.rollbar:rollbar:0.5.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
}

intellij {
    version 'IC-2021.1'
    updateSinceUntilBuild false
}

ext.tmpMarkdownInputDir = "${buildDir}/tmp/markdown-inputs".toString()
ext.htmlOutputDir = "${buildDir}/gen-html".toString()
ext.pluginVersion = '1.6'

task prepareMarkdownInputs(type: Copy) {
    from("${projectDir}") {
        include 'releasenotes.md'
    }
    into tmpMarkdownInputDir
}

markdownToHtml {
    dependsOn prepareMarkdownInputs
    sourceDir = new File(tmpMarkdownInputDir)
    outputDir = new File(htmlOutputDir)
    inputEncoding = 'UTF-8'
    outputEncoding = 'UTF-8'
}

sourceSets {
    main {
        java.srcDirs += 'gen'
        kotlin.srcDirs += 'gen'
    }
    test {
        java.srcDir 'src/test/java'
    }
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }

    beforeSuite { suite ->
        if (suite.className != null) {
            println()
            println(suite.className)
        }
    }
    afterTest { desc, result ->
        System.out.flush()
    }
    afterSuite { println() }
}

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateSoyLexer(type: GenerateLexer) {
    source 'src/main/grammars/Soy.flex'
    skeleton 'src/main/grammars/flex.skeleton'
    targetDir 'gen/com/google/bamboo/soy/lexer/'
    targetClass 'SoyFlexLexer'
    purgeOldFiles true
}

task generateSoyParser(type: GenerateParser) {
    source 'src/main/grammars/Soy.bnf'
    targetRoot 'gen'
    pathToParser 'com/google/bamboo/soy/parser/SoyParser.java'
    pathToPsiRoot 'com/google/bamboo/soy/parser/'
    purgeOldFiles true
}

compileJava {
    dependsOn generateSoyLexer
    dependsOn generateSoyParser
}

compileKotlin {
    dependsOn generateSoyLexer
    dependsOn generateSoyParser
}

def readChangeNotes() {
    def lines = new File(htmlOutputDir, 'releasenotes.html').readLines()

    // Drop the top-level "Release notes" header.
    return lines[1..lines.size-1].join('\n')
}

patchPluginXml {
    dependsOn markdownToHtml
    version pluginVersion
    changeNotes { readChangeNotes() }
}
